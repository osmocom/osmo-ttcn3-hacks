module RLAPD_CodecPort {

/* Simple RLAPD Codec Port, translating between raw TCP octetstring payload
 * towards the IPL4asp port provider, and RLAPD primitives
 * which carry the decoded RLAPD data types as payload.
 *
 * (C) 2020 by Harald Welte <laforge@gnumonks.org>
 * All rights reserved.
 *
 * Released under the terms of GNU General Public License, Version 2 or
 * (at your option) any later version.
 */


import from IPL4asp_PortType all;
import from IPL4asp_Types all;
import from LAPD_Primitives all;

type record RLAPD_RecvFrom {
	ConnectionId	connId,
	LAPD_Primitive	msg
}

type record RLAPD_Send {
	ConnectionId	connId,
	LAPD_Primitive	msg
}

template (value) RLAPD_Send ts_RLAPD_Send(ConnectionId conn_id, template (value) LAPD_Primitive msg) := {
	connId := conn_id,
	msg := msg
}

template RLAPD_RecvFrom tr_RLAPD_Recv(template ConnectionId conn_id, template LAPD_Primitive msg) := {
	connId := conn_id,
	msg := msg
}

private function IPL4_to_RLAPD_RecvFrom(in ASP_RecvFrom pin, out RLAPD_RecvFrom pout) {
	pout.connId := pin.connId;
	pout.msg := f_dec_LAPD_Primitive(pin.msg);
} with { extension "prototype(fast)" }

private function RLAPD_to_IPL4_Send(in RLAPD_Send pin, out ASP_Send pout) {
	pout.connId := pin.connId;
	pout.proto := { sctp := {} };
	pout.msg := f_enc_LAPD_Primitive(pin.msg);
} with { extension "prototype(fast)" }

type port RLAPD_CODEC_PT message {
	out	RLAPD_Send;
	in	RLAPD_RecvFrom,
		ASP_ConnId_ReadyToRelease,
		ASP_Event;
} with { extension "user IPL4asp_PT
	out(RLAPD_Send -> ASP_Send: function(RLAPD_to_IPL4_Send))
	in(ASP_RecvFrom -> RLAPD_RecvFrom: function(IPL4_to_RLAPD_RecvFrom);
	   ASP_ConnId_ReadyToRelease -> ASP_ConnId_ReadyToRelease: simple;
	   ASP_Event -> ASP_Event: simple)"
}



}
